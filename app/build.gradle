apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.3"
    defaultConfig {
        applicationId "com.udacity.popularmovie"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        // Add BuildConfig.TMDB_API_KEY field, get value from project gradle.properties file and if not defined from System env
        def propsHasApiKey = project.hasProperty('TMDB_API_KEY')
        def envApiKey = System.getenv('TMDB_API_KEY')
        def apiKey = propsHasApiKey ? TMDB_API_KEY : (envApiKey != null ? envApiKey : '"UNDEFINED-API-KEY"')
        buildConfigField "String", "TMDB_API_KEY", apiKey
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // support appcompat, constraint-layout, cardview, design(BottomNavigationView)
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:cardview-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'

    // Retrofit with JSON Parsing
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.google.code.gson:gson:2.8.2'

    // Picasso
    implementation 'com.squareup.picasso:picasso:2.5.2'

    // Timber
    implementation 'com.jakewharton.timber:timber:4.6.1'

    //EventBus
    implementation 'org.greenrobot:eventbus:3.1.1'

    // Stetho SQLite DEBUG bridge
    implementation 'com.facebook.stetho:stetho:1.5.0'
}
